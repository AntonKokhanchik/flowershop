buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


sourceCompatibility = 12
targetCompatibility = 12

repositories {
    mavenCentral()
}


dependencies {
    //  Spring
    compile project(':custom-starter')

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'

    //  Tomcat
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: '9.0.19'
    compile group: 'org.apache.tomcat', name: 'tomcat-annotations-api', version: '9.0.19'

    //  For views
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'

    //  Database
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jooq'

    //  web-service
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jersey'
    compile group: 'org.springframework.kafka', name: 'spring-kafka'
}

apply plugin: flowershop.greeting.GreetingPlugin

hello {
    message = 'hey'
    recipient = 'you'
}

def props = new Properties()
file("gradle.properties").withInputStream { props.load(it) }

ext {
    build_database = 'org.postgresql:postgresql:42.2.5'
}

// liquibase
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.liquibase:liquibase-gradle-plugin:2.0.1"
    }
}
apply plugin: 'org.liquibase.gradle'

dependencies {
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime "$build_database"
    liquibaseRuntime 'ch.qos.logback:logback-classic'
    liquibaseRuntime 'ch.qos.logback:logback-core'
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/changelog.xml'
            url props.getProperty('db.url')
            username props.getProperty('db.user')
            password props.getProperty('db.password')
            defaultSchemaName 'public'
        }
    }
    runList = 'main'
}

// jooq
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "nu.studer:gradle-jooq-plugin:3.0.3"
    }
}

apply plugin: "nu.studer.jooq"

dependencies {
    jooqRuntime "$build_database"
}

jooq {
    version = '3.11.11'
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver = props.getProperty('db.driver')
            url = props.getProperty('db.url')
            user = props.getProperty('db.user')
            password = props.getProperty('db.password')
        }
        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                deprecated = false
                instanceFields = true
                pojos = true
                daos = true
            }
        }
    }
}

// generate codes after db migration
generateSampleJooqSchemaSource.dependsOn update

// docker-compose
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.9.4"
    }
}

apply plugin: 'docker-compose'

dockerCompose {
    // запускаем во время билда вокруг генерации кода jooq
    postgres {
        startedServices = ['postgres']
        isRequiredBy(generateSampleJooqSchemaSource)
    }
    // запускаем во время исполнения
    kafka {
        startedServices = ['kafka']
        isRequiredBy(bootRun)
    }
}
